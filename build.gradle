plugins {
    id 'fabric-loom' version '1.7.4'
    id 'maven-publish'
    id 'io.freefair.lombok' version '8.10'
    id 'com.modrinth.minotaur' version '2.+'
}

version = mod_version
group = maven_group

base {
    archivesName = archives_base_name
}


repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    maven {
        name 'Xander Maven'
        url 'https://maven.isxander.dev/releases'
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "dev.isxander:yet-another-config-lib:${yacl_version}"
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
}

processResources {
    inputs.property "version", version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "loader_version", loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": version,
                "minecraft_version": minecraft_version,
                "loader_version": loader_version,
                "yacl_version": yacl_version,
                "modmenu_version": modmenu_version,
                "fabric_version": fabric_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
modrinth {
    token = System.getenv 'MODRINTH_TOKEN'
    projectId = 'minux'
    versionNumber = mod_version
    versionType = 'release'
    uploadFile = remapJar
    gameVersions = [minecraft_version]
    syncBodyFrom = rootProject.file("README.md").text
    dependencies {
        required.version 'fabric-api', fabric_version
        optional.version 'yacl', yacl_version
        optional.version 'modmenu', modmenu_version
    }
}
